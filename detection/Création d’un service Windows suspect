index="prod"
(
    (source="WinEventLog:Security" EventCode=4697)
    OR
    (source="WinEventLog:Microsoft-Windows-Sysmon/Operational"
        EventCode=1
        AND Image="*\\sc.exe"
    )
)
| eval suspicious_binpath = case(
        source="WinEventLog:Security"
        AND (
               like(Nom_de_fichier_du_service, "%\\Temp\\%")
            OR like(Nom_de_fichier_du_service, "%\\AppData\\%")
            OR like(Nom_de_fichier_du_service, "%\\Users\\Public%")
        ), 1,

        source="WinEventLog:Microsoft-Windows-Sysmon/Operational"
        AND (
               like(CommandLine, "%powershell%")
            OR like(CommandLine, "%mshta%")
            OR like(CommandLine, "%wscript%")
            OR like(CommandLine, "%cscript%")
            OR like(CommandLine, "%rundll32%")
            OR like(CommandLine, "%cmd.exe /c%")
            OR like(CommandLine, "%cmd.exe /k%")
            OR like(CommandLine, "%C:\\Users\\Public%")
            OR like(CommandLine, "%\\Downloads\\%")
            OR like(CommandLine, "%\\Desktop\\%")
            OR like(CommandLine, "%C:\\Windows\\Temp\\%")
            OR like(CommandLine, "%\\AppData\\Local\\Temp%")
        ), 1,

        true(), 0
)
| where suspicious_binpath=1
| eval ServiceName = coalesce(Nom_du_service, ServiceName)
| rex field=CommandLine "create\s+(?<ServiceName>[^\s]+)"
| eval BinPath = coalesce(Nom_de_fichier_du_service, BinPath)
| rex field=CommandLine "binPath=\s+\"(?<BinPath>[^\"]+)\""
| eval Account = coalesce(Nom_du_compte, User)
| eval Message = coalesce(CommandLine, BinPath, ServiceName)
| eval alert_title="Création de service suspecte"
| eval alert_description="Création de service Windows potentiellement malveillante"
| eval alert_source_event_time = strftime(_time, "%Y-%m-%d %H:%M:%S")
| rename sourcetype AS alert_source
| eval alert_severity_id=3
| rename Message AS alert_source_content
| table host alert_title alert_description alert_source alert_source_event_time alert_severity_id alert_source_content
